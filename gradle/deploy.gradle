jar {
  manifest {
    attributes(
      "Automatic-Module-Name": project.group + '.' + project.name,
      "Implementation-Title": project.group + '.' + project.name,
      "Implementation-Version": archiveVersion,
      "Implementation-Vendor": "https://asolntsev.github.io/")
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

javadoc {
  failOnError = false
  source = sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

if (project.hasProperty("signing.keyId")) {
  apply plugin: 'signing'
  apply plugin: 'maven-publish'

  signing {
    afterEvaluate {
      sign publishing.publications.mavenJava
    }
  }

  artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
  }

  publishing {
    repositories {
      maven {
        name 'Maven'
        url project.version.endsWith("-SNAPSHOT") ?
                'https://oss.sonatype.org/content/repositories/snapshots/' :
                'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
        credentials {
          username "$sonatypeUsername"
          password "$sonatypePassword"
        }
      }
    }
    publications {
      mavenJava(MavenPublication) {
        groupId "${project.group}"
        artifactId "${project.name}"

        artifact(jar)
        artifact(sourcesJar)
        artifact(javadocJar)

        pom {
          name = archivesBaseName
          description = 'Java client for Nexus repository manager'
          url = 'https://github.com/asolntsev/nexus-repository-manager'
          licenses {
            license {
              name = 'MIT'
              url = 'https://opensource.org/licenses/MIT'
            }
          }
          developers {
            developer {
              id = 'asolntsev'
              name = 'Andrei Solntsev'
            }
          }
          scm {
            connection = 'scm:git@github.com:asolntsev/nexus-repository-manager.git'
            developerConnection = 'scm:git@github.com:asolntsev/nexus-repository-manager.git'
            url = 'https://github.com/asolntsev/nexus-repository-manager'
          }
        }
      }
    }
  }
}
